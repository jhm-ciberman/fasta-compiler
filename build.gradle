plugins {
    id 'java'
    id 'application'
}

group 'com.ciberman.fastacompiler'
version '1.4.2'

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

application {
    mainClassName = 'com.ciberman.fastacompiler.Main'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task parser(type:Exec) {
    def srcDir = './src/main/java/com/ciberman/fastacompiler/parser/'
    inputs.files([
            srcDir + 'grammar.y',
            srcDir + 'generate.bat',
    ])
    outputs.files([
            srcDir + 'Parser.java',
            srcDir + 'ParserVal.java',
    ])

    workingDir srcDir
    commandLine 'cmd', '/c', 'generate.bat'
}

tasks.compileJava.dependsOn(parser)



dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    implementation group: 'org.jetbrains', name: 'annotations', version: '20.1.0'

    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.0'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-params
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.7.0'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.0'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
    testImplementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

}